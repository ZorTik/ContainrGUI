plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "4.0.4"
    id 'maven-publish'
}

compileJava.options.encoding = 'UTF-8'

group = 'com.github.ZorTik'
version '1.0-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
        maven {
            name = 'spigotmc-repo'
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        maven {
            name = 'NBT-API'
            url = 'https://repo.codemc.org/repository/maven-public/'
        }
        maven {
            name = 'minecraft'
            url = 'https://libraries.minecraft.net/'
        }
    }
}

dependencies {
    implementation project(':core')
    implementation group: 'org.jetbrains', name: 'annotations', version: '20.1.0'
    implementation 'de.tr7zw:item-nbt-api:2.12.0'
    compileOnly 'io.projectreactor:reactor-core:3.5.2' // Should be included in using projects.
    compileOnly 'commons-lang:commons-lang:2.6'
    compileOnly 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.21'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.19.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation project(':core')
    testImplementation group: 'org.jetbrains', name: 'annotations', version: '20.1.0'
    testImplementation 'de.tr7zw:item-nbt-api:2.12.0'
    testImplementation 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'
    testCompileOnly 'io.projectreactor:reactor-core:3.5.2' // Should be included in using projects.
    testCompileOnly 'commons-lang:commons-lang:2.6'
    testCompileOnly 'com.mojang:authlib:1.5.21'
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveName = "$baseName-$version.$extension"
    relocate('de.tr7zw.changeme', 'me.zort.containr.libs.de.tr7zw')
    relocate('de.tr7zw.annotations', 'me.zort.containr.libs.de.tr7zw.annotations')
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

tasks.withType(org.gradle.jvm.tasks.Jar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

artifacts {
    archives shadowJar
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    compileJava.options.encoding = 'UTF-8'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = 'com.github.ZorTik'
                artifactId = project.name
                version = '1.0'

                from components.java
                pom {
                    name = 'ContainrGUI'
                    description = 'An Advanced GUI library for Spigot.'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://raw.githubusercontent.com/ZorTik/ContainrGUI/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'zort'
                            name = 'ZorTik'
                            email = 'zortlegit@gmail.com'
                        }
                    }
                }
            }
        }
    }
}
